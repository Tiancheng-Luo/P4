%  This file is part of P4
% 
%  Copyright (C) 1996-2017  J.C. Art√©s, P. De Maesschalck, F. Dumortier,
%                           C. Herssens, J. Llibre, O. Saleta, J. Torregrosa
% 
%  P4 is free software: you can redistribute it and/or modify
%  it under the terms of the GNU Lesser General Public License as published
%  by the Free Software Foundation, either version 3 of the License, or
%  (at your option) any later version.
% 
%  This program is distributed in the hope that it will be useful,
%  but WITHOUT ANY WARRANTY; without even the implied warranty of
%  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
%  GNU Lesser General Public License for more details.
% 
%  You should have received a copy of the GNU Lesser General Public License
%  along with this program.  If not, see <http://www.gnu.org/licenses/>.
       
\documentclass[a4paper,10pt]{article}
\usepackage{listings}
\usepackage{color}
\input{mapledef}

\title{P4 Maple routines:\\P4 GCF}
\author{}
\date{}

\setlength\marginparwidth{0cm}
\setlength\marginparsep{0cm}
\setlength\oddsidemargin{2cm}
\setlength\textwidth{\paperwidth}
\addtolength\textwidth{-2\oddsidemargin}
\addtolength\oddsidemargin{-1in}

\begin{document}
\maketitle

\section{Overview}

This file is used by P4 when a Greatest common factor needs to be evaluated numerically.
It gives a set of points that are determined by $f(x,y)=0$, within a given rectangle $[x_1,x_2]\times[y_1,y_2]$.
The routine to be called is \emph{FindSingularities}, and afterwards, the value \emph{returnvalue} is either
$0$ or $1$, depending on whether an error occurred or not (0=success, 1=failure).


From Maple 10, a compatibility problem has added to needed to include a ``SimplifyPlot'' procedure inside the implementation.
This changes any occuring Arrays in the plot structure to lists or double lists.

\section{Implementation}

Default values of the user parameters:

\begin{lstlisting}[name=p4gcf]
restart;
user_numpoints := 100:
u := x:
v := y:
user_f := x*y:
user_file := "untitled_gcf.tab":
user_x1 := -1:
user_x2 := 1:
user_y1 := -1:
user_y2 := 1:
returnvalue := 0:
\end{lstlisting}

In the program, one sometimes uses the shortcuts $u$ and $v$ for $x\cos y$ and $x\sin y$.  Therefore,
we leave an opening so that these variables can be used.

The main routine:

\begin{lstlisting}[name=p4gcf]
FindSingularities := proc()
    global  user_numpoints, u, v, user_f, user_file,
            user_x1, user_x2, user_y1, user_y2, returnvalue, facs, fac;
    local   m, n, P, k, i, j, fp, s, f, SimplifyPlot, CURVES_ARRAY_TO_LISTLIST;

    CURVES_ARRAY_TO_LISTLIST := proc()
        local ss, k;
        ss := [$1..nargs];
        for k from 1 to nargs do
            ss[k] := args[k];
            if type( ss[k], Array ) then
                if ArrayNumDims( ss[k] ) = 2 then
                    ss[k] := convert( ss[k], listlist );
                elif ArrayNumDims( ss[k] ) = 1 then
                    ss[k] := convert( ss[k], list );
                end if;
            end if;
        end do;
        CURVES(op(ss));
    end proc;
    
    SimplifyPlot := P -> eval( P, CURVES=CURVES_ARRAY_TO_LISTLIST );
    
    returnvalue := 1;
    if degree( user_f, [x,y,U,V] ) <= 0 then
        printf( "Greatest common factor is a constant.\n" );
        fp = fopen( user_file, WRITE, TEXT );
        fclose( fp );
        k:=0;
    else
        m := round( sqrt(user_numpoints) ) + 1:
        n := m:
        facs := factors(subs({U=u,V=v},user_f))[2];
        P := map( fac -> plots[implicitplot]( fac,
            x=user_x1..user_x2,y=user_y1..user_y2,axes=none,
            view=[user_x1..user_x2,user_y1..user_y2],grid=[m,n],color=black), facs ):
        P := map( SimplifyPlot, P );
        P := map( fac -> op(select( pt -> type(pt,list), [op(op(1,fac))] )), P ):
        k := 0:
        fp := fopen( user_file, WRITE, TEXT );
        for i from 1 to nops(P) do
            for j from 1 to nops(P[i]) do
                s := sprintf( "%g %g ", P[i,j,1], P[i,j,2] );
                fprintf( fp, "%s", s );
                k := k+1;
            end do;
            if i <> nops(P) then
                fprintf( fp, ",\n" );
            else
                fprintf( fp, "\n" );
            end if;
        end do:
        fclose( fp );
    end if;
    printf( "%d points returned.\n", k );
    returnvalue := 0;
    NULL;
    end:
\end{lstlisting}

Saving all to a library:

\begin{lstlisting}[name=p4gcf]
save( user_numpoints, u, v, user_f, user_file, user_x1, user_x2, user_y1, user_y2,
      returnvalue, FindSingularities, "p4gcf.m" );
\end{lstlisting}

\end{document}
